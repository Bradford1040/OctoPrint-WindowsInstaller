; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OctoPrint"
#define MyAppVersion "1.8.1"
#define MyAppPublisher "OctoPrint"
#define MyAppURL "https://www.octoprint.org/"
#define MyAppExeName "octoprint.exe" 

#define public Dependency_NoExampleSetup
#include "CodeDependencies.iss"  

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{333BC575-A27C-4E6C-BE2B-59E5AEE715F3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=False
OutputDir=Output
OutputBaseFilename=OctoPrint Setup
SetupIconFile=OctoPrint.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
DisableReadyPage=True
UninstallDisplayIcon={app}\OctoPrint.ico    
WizardImageFile=WizModernImage-OctoPrint*.bmp
WizardSmallImageFile=WizModernSmallImage-OctoPrint*.bmp
DisableWelcomePage=False
DisableDirPage=False

[Run]
Filename: "{app}\WPy64-31040\scripts\upgrade_pip.bat"; WorkingDir: "{app}"; Flags: runhidden shellexec waituntilidle; StatusMsg: "Updating PIP"
Filename: "{app}\WPy64-31040\Scripts\python.bat"; Parameters: "-m pip install octoprint"; WorkingDir: "{app}"; Flags: runhidden; StatusMsg: "Installing Latest OctoPrint into WinPython"; AfterInstall: update_service_config
;Filename: "{app}\OctoPrintService{#OctoPrintPort}.exe"; Parameters: "install --no-elevate"; WorkingDir: "{sys}"; Flags: runhidden; StatusMsg: "Creating OctoPrint Service"
;Filename: "{app}\OctoPrintService{OctoPrintPort}.exe"; Parameters: "start"; WorkingDir: "{sys}"; Flags: postinstall waituntilidle runhidden; Description: "Start OctoPrint Service"; StatusMsg: "Start OctoPrint Service"
Filename: "{app}\Service Control\"; WorkingDir: "{app}\Service Control\"; Flags: shellexec runasoriginaluser postinstall nowait; Description: "Open OctoPrint Service Control Folder to Install and Manage Services"

[UninstallRun]
;Filename: "{app}\OctoPrintService{code: GetOctoPrintPort}.exe"; Parameters: "stop --no-elevate --no-wait --force"; WorkingDir: "{app}"; Flags: runhidden
;Filename: "{app}\OctoPrintService{code: GetOctoPrintPort}.exe"; Parameters: "uninstall --no-elevate"; WorkingDir: "{app}"; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}\*"

[Code]
function InitializeSetup: Boolean; 
begin 
  Dependency_AddVC2013;
  Result := True;          
end;

var
  InputQueryWizardPage: TInputQueryWizardPage;
  DataDirPage: TInputDirWizardPage;
  WrapperPath: String;
  OctoPrintPort: String;
  OctoPrintBasedir: String;
  InstallWinPython: Boolean;

procedure InitializeWizard;
begin
// OctoPrint Port Dialog Page     
  InputQueryWizardPage := CreateInputQueryPage(wpWelcome, 'OctoPrint Setup', 'What port should OctoPrint use?', 'Enter the port that OctoPrint will listen on for web connections, then click Next.');
  InputQueryWizardPage.Add('Port:', False);
  InputQueryWizardPage.Values[0] := GetPreviousData('OctoPrintPort', '5000');
  
// OctoPrint Basedir Selection Page  
  DataDirPage := CreateInputDirPage(wpSelectDir,
    'OctoPrint Setup', 'Where should OctoPrint data files be installed?',
    'Select the folder in which OctoPrint will store uploads, configs, and other data files, then click Next.',
    False, '');
  DataDirPage.Add('Basedir Path:');
  DataDirPage.Values[0] := GetPreviousData('DataDir', WizardDirValue() + '\basedir');

// Initialize contstants
  OctoPrintPort := InputQueryWizardPage.Values[0];  
  WrapperPath := WizardDirValue() + '\OctoPrintService' + OctoPrintPort + '.exe';
end;

procedure rename_config();
var
  UnicodeStr: string;
  ANSIStr: AnsiString;
begin
  if LoadStringFromFile(ExpandConstant(CurrentFilename), ANSIStr) then
  begin
    UnicodeStr := String(ANSIStr);
    if StringChangeEx(UnicodeStr, '####APPDIR####', WrapperPath, True) > 0 then
      if DirExists(ExpandConstant(OctoPrintBasedir)) = False then
        ForceDirectories(ExpandConstant(OctoPrintBasedir));
      SaveStringToFile(ExpandConstant(OctoPrintBasedir + '\config.yaml'), AnsiString(UnicodeStr), False);
  end;
end; 

procedure rename_service_wrapper();
var
  FolderPath: string;
begin
  FolderPath := ExpandConstant('{app}\Service Control\' + OctoPrintPort);
  FileCopy(ExpandConstant(CurrentFilename), WrapperPath, False); 
  ForceDirectories(FolderPath);
  CreateShellLink(FolderPath + '\Install OctoPrint Service.lnk', 'Install the OctoPrint service on port ' + OctoPrintPort, ExpandConstant(WrapperPath), 'install', ExpandConstant('{app}'), ExpandConstant('{app}\OctoPrint.ico'), 0, SW_SHOWNORMAL);  
  CreateShellLink(FolderPath + '\Restart OctoPrint Service.lnk', 'Restart the OctoPrint service on port ' + OctoPrintPort, ExpandConstant(WrapperPath), 'restart!', ExpandConstant('{app}'), ExpandConstant('{app}\OctoPrint.ico'), 0, SW_SHOWNORMAL);       
  CreateShellLink(FolderPath + '\Start OctoPrint Service.lnk', 'Start the OctoPrint service on port ' + OctoPrintPort, ExpandConstant(WrapperPath), 'start', ExpandConstant('{app}'), ExpandConstant('{app}\OctoPrint.ico'), 0, SW_SHOWNORMAL);      
  CreateShellLink(FolderPath + '\Stop OctoPrint Service.lnk', 'Stop the OctoPrint service on port ' + OctoPrintPort, ExpandConstant(WrapperPath), 'stop', ExpandConstant('{app}'), ExpandConstant('{app}\OctoPrint.ico'), 0, SW_SHOWNORMAL);       
  CreateShellLink(FolderPath + '\Uninstall OctoPrint Service.lnk', 'Uninstall the OctoPrint service on port ' + OctoPrintPort, ExpandConstant(WrapperPath), 'uninstall', ExpandConstant('{app}'), ExpandConstant('{app}\OctoPrint.ico'), 0, SW_SHOWNORMAL);
end;

procedure update_service_config(); 
var
  UnicodeStr: string;
  ANSIStr: AnsiString;
begin
  if LoadStringFromFile(ExpandConstant('{app}\OctoPrintService.xml'), ANSIStr) then
  begin
    UnicodeStr := String(ANSIStr);
    StringChangeEx(UnicodeStr, '####EXEPATH####', ExpandConstant('{app}\WPy64-31040\python-3.10.4.amd64\Scripts\octoprint.exe'), True) 
    StringChangeEx(UnicodeStr, '####BASEDIR####', DataDirPage.Values[0], True) 
    StringChangeEx(UnicodeStr, '####PORT####', InputQueryWizardPage.Values[0], True)
    SaveStringToFile(ExpandConstant('{app}\OctoPrintService' + OctoPrintPort + '.xml'), AnsiString(UnicodeStr), False);
  end;
end;

function GetServiceWrapperPath(Param: string): String;
begin
  Result := WrapperPath;
end;

function GetOctoPrintPort(Param: string): String;
begin
  Result := OctoPrintPort;
end; 

function GetOctoPrintBasedir(Param: string): String;
begin
  Result := OctoPrintBasedir;
end;

function NextButtonClick(CurPageID: Integer): Boolean;
begin   
  if CurPageID = InputQueryWizardPage.ID then 
  begin
    OctoPrintPort := InputQueryWizardPage.Values[0];
    WrapperPath := WizardDirValue() + '\OctoPrintService' + OctoPrintPort + '.exe';
  end;
  if CurPageID = wpSelectDir then 
  begin
    DataDirPage.Values[0] := WizardDirValue() + '\basedir\' + OctoPrintPort;
    WrapperPath := WizardDirValue() + '\OctoPrintService' + OctoPrintPort + '.exe';
    OctoPrintBasedir := DataDirPage.Values[0];
  end;
  if CurPageID = DataDirPage.ID then
  begin
    OctoPrintBasedir := DataDirPage.Values[0];
  end;
  Result := True;
end;

function WinPythonInstalled(DirName: String): Boolean;
begin
  if InstallWinPython then begin
    Result := True; 
  end else begin
    InstallWinPython := DirExists(DirName) = False;
    Result := False;
  end;
end;

procedure RegisterPreviousData(PreviousDataKey: Integer);
begin
  { Store the settings so we can restore them next time }
  SetPreviousData(PreviousDataKey, 'DataDir', DataDirPage.Values[0]);  
  SetPreviousData(PreviousDataKey, 'OctoPrintPort', InputQueryWizardPage.Values[0]);
end;

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "WPy64-31040\*"; DestDir: "{app}\WPy64-31040"; Flags: recursesubdirs createallsubdirs ignoreversion; Check: WinPythonInstalled(ExpandConstant('{src}'))
Source: "OctoPrint.ico"; DestDir: "{app}"
Source: "OctoPrintService.exe"; DestDir: "{app}"; AfterInstall: rename_service_wrapper
Source: "OctoPrintService.xml"; DestDir: "{app}"
Source: "config.yaml"; DestDir: "{app}"; AfterInstall: rename_config

[Icons]
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "http://localhost:5000/"; IconFilename: "{app}\OctoPrint.ico"; IconIndex: 0
Name: "{group}\{cm:ProgramOnTheWeb,OctoPrint Website}"; Filename: "{#MyAppURL}"
;Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
