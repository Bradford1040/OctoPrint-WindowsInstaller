; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "OctoPrint"
#define MyAppVersion "1.8.1"
#define MyAppPublisher "OctoPrint"
#define MyAppURL "https://www.octoprint.org/"
#define MyAppExeName "octoprint.exe"

#define public Dependency_NoExampleSetup
#include "CodeDependencies.iss"

[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{333BC575-A27C-4E6C-BE2B-59E5AEE715F3}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName=C:\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=no
OutputDir=Output
OutputBaseFilename=OctoPrint Setup
SetupIconFile=logo.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern
WizardImageStretch=False

[Run]
Filename: "{app}\WPy64-31040\scripts\python.bat"; Parameters: "-m venv {app}\venv"; WorkingDir: "{app}"; Flags: runhidden; StatusMsg: "Creating Virtual Environment"
Filename: "{app}\venv\Scripts\activate.bat"; WorkingDir: "{app}"; Flags: runhidden; StatusMsg: "Activating venv"
Filename: "{app}\venv\Scripts\pip"; Parameters: "install octoprint"; WorkingDir: "{app}"; Flags: runhidden; StatusMsg: "Installing OctoPrint into venv"
Filename: "{sys}\sc.exe"; Parameters: "create OctoPrint start= auto binPath= ""{app}\venv\Scripts\octoprint.exe serve"""; WorkingDir: "{sys}"; Flags: runhidden; StatusMsg: "Creating OctoPrint Service"
Filename: "{sys}\sc.exe"; Parameters: "start OctoPrint"; WorkingDir: "{sys}"; Flags: runhidden postinstall; Description: "Start OctoPrint Service"; StatusMsg: "Start OctoPrint Service"

[UninstallRun]
Filename: {sys}\sc.exe; Parameters: "stop OctoPrint" ; Flags: runhidden
Filename: {sys}\sc.exe; Parameters: "delete OctoPrint" ; Flags: runhidden

[UninstallDelete]
Type: filesandordirs; Name: "{app}\*"

[Code]
function InitializeSetup: Boolean;
begin
  Dependency_AddVC2013;
  Result := True;
end;

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "WPy64-31040\*"; DestDir: "{app}\WPy64-31040"; Flags: ignoreversion recursesubdirs createallsubdirs
; NOTE: Don't use "Flags: ignoreversion" on any shared system files
Source: "logo.ico"; DestDir: "{app}"

[Icons]
Name: "{group}\{cm:ProgramOnTheWeb,{#MyAppName}}"; Filename: "{#MyAppURL}"; IconFilename: "{app}\logo.ico"
Name: "{group}\{cm:UninstallProgram,{#MyAppName}}"; Filename: "{uninstallexe}"
